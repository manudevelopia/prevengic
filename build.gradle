
plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.google.cloud.tools.jib' version '1.2.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'info.developia.prevengic'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    compile 'org.mapstruct:mapstruct-jdk8:1.3.0.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.0.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'
    testCompile 'org.spockframework:spock-spring:1.2-groovy-2.5'
    testCompile 'io.rest-assured:rest-assured:3.2.0'
}

task installJsDependencies(type: Exec) {
    commandLine "npm", "install"
}

task webpack(type: Exec) {
    dependsOn installJsDependencies
    commandLine "$projectDir/node_modules/.bin/webpack"
}

build.dependsOn webpack

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}